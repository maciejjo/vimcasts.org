--- 
:title: Using external filter commands to reformat HTML
:date: 2014/02/18
:poster: /images/posters/formatprg-pandoc.png
:flattr_id: ""
:duration: 271
:number: 64
:tags: html, external-filters
---

We can use pandoc as a [filter][] to clean up <abbr title="what you see is what you get">WYSIWYG</abbr>-generated HTML. Pandoc is a commandline program, but we can call it from inside Vim either using the [bang Ex command][bang], or by configuring [the `formatprg` option][formatprg] to make the `gq` operator invoke pandoc.

[filter]: http://vimdoc.sourceforge.net/htmldoc/change.html#filter
[formatprg]: http://vimdoc.sourceforge.net/htmldoc/options.html#'formatprg'
[gq]: http://vimdoc.sourceforge.net/htmldoc/change.html#gq
[bang]: http://vimdoc.sourceforge.net/htmldoc/various.html#:!


READMORE

<r:snippet name="announcement"/>

I recently saw [a neat trick][hay1] on Twitter from Stephen Hay, who says that:

> Few things clean up CMS-input HTML better than running it through Pandoc to convert to Markdown and then back to HTML again. 1 sec, big win.

[pandoc][] is a swiss-army knife for converting between all sorts of markup formats. You can find [installation instructions on the pandoc site][installing-pandoc].

Suppose we have a `tea-dance.html` file that contains crufty markup, because it was generated by a <abbr title="what you see is what you get">WYSIWYG</abbr> editor. We could clean it up by running this at the command line:

    cat tea-dance.html | pandoc --from=html --to=markdown | pandoc --from=markdown --to=html

This emits a cleaned up version of `tea-dance.html` on standard out.

We're using pandoc as a [filter][], that is: a program "that accepts text at standard input, changes it in some way, and sends it to standard output".

### Running text from a Vim buffer through an external filter

Suppose that we open the `tea-dance.html` file in Vim. We can use the [bang Ex command][bang] to filter the contents of the current buffer through our pandoc pipeline:

    :%!pandoc --from=html --to=markdown | pandoc --from=markdown --to=html

Vim will take the output from that pipeline and use it to overwrite the original text from the buffer.

In a [followup tweet][hay2], Stephen suggests mapping this Ex command to a key so we can run it more easily.
For example, you could add a mapping for normal mode and another for visual mode:

```viml
nnoremap <leader>gq :%!pandoc -f html -t markdown | pandoc -f markdown -t html<CR>
vnoremap <leader>gq :!pandoc -f html -t markdown | pandoc -f markdown -t html<CR>
```

That'll work, but I want to suggest a way of doing it [without leader mappings][follow-my-leader].

### Set up formatprg to filter selection through pandoc

In [episode 18](/e/18) of Vimcasts, I demonstrated how the external `par` command could be used for the task of formatting plain text files with hard-wrapping.
We could use a similar technique here.

The [`gq`][gq] operation runs the selected text through the filter specified by [`formatprg`][formatprg].
This autocommand sets `formatprg` for HTML files to use our pandoc pipeline:

```viml
if has("autocmd")
  let pandoc_pipeline  = "pandoc --from=html --to=markdown"
  let pandoc_pipeline .= " | pandoc --from=markdown --to=html"
  autocmd FileType html let &formatprg=pandoc_pipeline
endif
```

That means we can filter the current line through pandoc by pressing `gqq`.
Or we can filter the entire buffer by pressing `gg` then `gqG`.
Or we can switch to visual mode, and `gq` will filter only the selected lines.

<h3 id='update'>Update: use `formatexpr` instead</h3>

As domo pointed out in the comments, the `formatprg` setting can only be set globally. That's unfortunate! [Kana suggested using `formatexpr` instead][kana], so I came up with this alternative:

```viml
function! FormatprgLocal(filter)
if !empty(v:char)
  return 1
else
  let l:command = v:lnum.','.(v:lnum+v:count-1).'!'.a:filter
  echo l:command
  execute l:command
endif
endfunction

if has("autocmd")
  let pandoc_pipeline  = "pandoc --from=html --to=markdown"
  let pandoc_pipeline .= " | pandoc --from=markdown --to=html"
  autocmd FileType html setlocal formatexpr=FormatprgLocal(pandoc_pipeline)
endif
```

However, this approach has a setback of its own. According to Sung Pae in the discussion for [this patch to convert formatprg to a global-local option][every-stroke]:

> formatexpr` is called on every keystroke in Insert mode, even when
> `formatoptions` is empty. This suggests to me that the primary purpose of
> `formatexpr` is for fine-grained control over automatic formatting, rather
> than to be a simple, on demand paragraph formatter like `fmt` or `par`

Sung Pae's patch makes `formatprg` global-local, which means that it could be configured independently for differnt filetypes. It would be great if that patch could be folded into core Vim!

### Further reading

* Stephen Hay's tweets: [one][hay1] and [two][hay2]
* [pandoc][]
* [Installing pandoc][installing-pandoc]
* [`:h filter`][filter]
* [`:h :range!`][bang]
* [`:h formatprg`][formatprg]
* [`:h gq`][gq]

[hay1]: https://twitter.com/stephenhay/status/413659466211397633
[hay2]: https://twitter.com/stephenhay/status/413668124051775488
[pandoc]: http://johnmacfarlane.net/pandoc/
[installing-pandoc]: http://johnmacfarlane.net/pandoc/installing.html
[filter]: http://vimdoc.sourceforge.net/htmldoc/change.html#filter
[formatprg]: http://vimdoc.sourceforge.net/htmldoc/options.html#'formatprg'
[gq]: http://vimdoc.sourceforge.net/htmldoc/change.html#gq
[follow-my-leader]: http://vimcasts.org/blog/2014/02/follow-my-leader/
[bang]: http://vimdoc.sourceforge.net/htmldoc/various.html#:!
[every-stroke]: https://groups.google.com/forum/#!msg/vim_dev/cFK1UjstyAk/mreb2H4VCtoJ
[kana]: https://twitter.com/kana1/status/435717948435484672
